/*
 *  This file is part of MinePass, licensed under the MIT License (MIT).
 *
 *  Copyright (c) MinePass.net <http://www.minepass.net>
 *
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to deal
 *  in the Software without restriction, including without limitation the rights
 *  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 *  copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 *  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 *  THE SOFTWARE.
 */

import org.apache.tools.ant.filters.ReplaceTokens

loadSpigotConfiguration()

def loadSpigotConfiguration() {
    project.ext['spigotEnv'] = hasProperty('env') ? env : 'development'
    println "Spigot Environment is set to ${project.spigotEnv}"

    project.ext.coreVersion = '2.0-SNAPSHOT'
    println "Spigot MP-Core Version is set to ${project.coreVersion}"

    project.ext['spigotVersion'] = hasProperty('spigot') ? spigot : '1.8-R0.1-SNAPSHOT'
    println "Spigot Version is set to ${project.spigotVersion}"
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.18.42' + "-" + (System.getenv("BUILD_NUMBER") ?: "SNAPSHOT")
group = "net.minepass"
archivesBaseName = "MinePass-Spigot[1.8-1.10]"

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

configurations {
    minepass_core
    compile {
        extendsFrom minepass_core
    }
}

dependencies {
    compile "org.bukkit:bukkit:${project.ext.spigotVersion}"

    if ( project.spigotEnv == 'core_dev' ) {
        minepass_core(project(":minepass_core")) {
            transitive = true
        }
    } else if ( project.spigotEnv == 'development' ) {
        minepass_core group: 'net.minepass', name: 'minepass-gameserver', version: "${project.coreVersion}.jar", changing: true
    } else if (project.spigotEnv == 'production') {
        minepass_core files("minepass-gameserver-${project.coreVersion}.jar")
    }
}

processResources {
    filter ReplaceTokens, tokens:[version: project.version]
}

task extractCore(type: Copy) {
    from {  // use of closure defers evaluation until execution time
        configurations.minepass_core.collect { zipTree(it) }
    }
    into "$buildDir/classes/main/"
}

classes.dependsOn extractCore

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

